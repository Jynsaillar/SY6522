cmake_minimum_required(VERSION 3.14)

set(SY6522_known_comps static shared)
set(SY6522_comp_static NO)
set(SY6522_comp_shared NO)
foreach (SY6522_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (SY6522_comp IN_LIST SY6522_known_comps)
        set(SY6522_comp_${SY6522_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "SY6522 does not recognize component `${SY6522_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (SY6522_comp_static AND SY6522_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "SY6522 `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(SY6522_static_targets "${CMAKE_CURRENT_LIST_DIR}/SY6522-static-targets.cmake")
set(SY6522_shared_targets "${CMAKE_CURRENT_LIST_DIR}/SY6522-shared-targets.cmake")

macro(SY6522_load_targets type)
    if (NOT EXISTS "${SY6522_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "SY6522 `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${SY6522_${type}_targets}")
endmacro()

if (SY6522_comp_static)
    SY6522_load_targets(static)
elseif (SY6522_comp_shared)
    SY6522_load_targets(shared)
elseif (DEFINED SY6522_SHARED_LIBS AND SY6522_SHARED_LIBS)
    SY6522_load_targets(shared)
elseif (DEFINED SY6522_SHARED_LIBS AND NOT SY6522_SHARED_LIBS)
    SY6522_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${SY6522_shared_targets}")
        SY6522_load_targets(shared)
    else ()
        SY6522_load_targets(static)
    endif ()
else ()
    if (EXISTS "${SY6522_static_targets}")
        SY6522_load_targets(static)
    else ()
        SY6522_load_targets(shared)
    endif ()
endif ()
